Trivial mixer using Skywater models.

** opencircuitdesign pdks install
.lib /home/ttuser/pdk/sky130A/libs.tech/ngspice/sky130.lib.spice tt

* The power supply.
Vdd vdd 0 1.8
Vss vss 0 0
* The RF bias
*Vrfin rfin 0 0.8
Vrfin rfin 0 sin(0.8 1m 7MEG)
* The LO bias
* PULSE ( V1 V2 TD TR TF PW PER NP )
*Vlo_p lo_p 0 1.2
*Vlo_m lo_m 0 1.2
Vlo_p lo_p 0 pulse(0 0.8 0 2n 2n .28u .56u)
Vlo_m lo_m 0 pulse(0.8 0 0 2n 2n .28u .56u)

* Transconductance Stage

* Notice that the MOSFET is represented as a subcircuit (starts with X!)
* Drain/Gate/Source/Body
XMn1 v1 rfin vss vss sky130_fd_pr__nfet_01v8 
+ L=0.15 W=1.5 nf=1 
+ ad='int((nf+1)/2) * W/nf * 0.29' 
+ as='int((nf+2)/2) * W/nf * 0.29' 
+ pd='2*int((nf+1)/2) * (W/nf + 0.29)'
+ ps='2*int((nf+2)/2) * (W/nf + 0.29)' 
+ nrd='0.29 / W' 
+ nrs='0.29 / W' 
+ sa=0 sb=0 sd=0 mult=1 m=1

* Switching Stage

XMn2_p v2_p lo_p v1 vss sky130_fd_pr__nfet_01v8 
+ L=0.15 W=1.5 nf=1 
+ ad='int((nf+1)/2) * W/nf * 0.29' 
+ as='int((nf+2)/2) * W/nf * 0.29' 
+ pd='2*int((nf+1)/2) * (W/nf + 0.29)'
+ ps='2*int((nf+2)/2) * (W/nf + 0.29)' 
+ nrd='0.29 / W' 
+ nrs='0.29 / W' 
+ sa=0 sb=0 sd=0 mult=1 m=1

XMn2_m v2_m lo_m v1 vss sky130_fd_pr__nfet_01v8 
+ L=0.15 W=1.5 nf=1 
+ ad='int((nf+1)/2) * W/nf * 0.29' 
+ as='int((nf+2)/2) * W/nf * 0.29' 
+ pd='2*int((nf+1)/2) * (W/nf + 0.29)'
+ ps='2*int((nf+2)/2) * (W/nf + 0.29)' 
+ nrd='0.29 / W' 
+ nrs='0.29 / W' 
+ sa=0 sb=0 sd=0 mult=1 m=1

* Output Stage
R3_p vdd v2_p 5000
R3_m vdd v2_m 5000

.control
; Delete all of the plots
destroy all
.options savecurrents
tran 100p 2000n

; ----- Plots ------

set xbrushwidth=2
*print v(v1)
*print @m.xmn1.msky130_fd_pr__nfet_01v8[id]
*print @m.xmn2_p.msky130_fd_pr__nfet_01v8[id]
*print @m.xmn2_m.msky130_fd_pr__nfet_01v8[id]
*print v(v2_p) @r3_p[i] v(v2_m) @r3_m[i]

*plot v(v1) v(v2_p) v(v2_m)
*plot @m.xmn1.msky130_fd_pr__nfet_01v8[id] @m.xmn2_p.msky130_fd_pr__nfet_01v8[id] @m.xmn2_m.msky130_fd_pr__nfet_01v8[id]

plot v(v2_p) v(v2_m)

.endc

.end
